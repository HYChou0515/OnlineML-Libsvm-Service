buildscript {
    repositories {
        maven { url 'http://repo1.maven.org/maven2' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.5.RELEASE")
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6"
    }
}
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'

group 'io.hychou'
version '0.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url 'http://repo1.maven.org/maven2' }
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.1.0.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.1.0.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf', version: '2.1.0.RELEASE'

    // Spring AOP with AspectJ
    compile group: 'org.springframework', name: 'spring-aop', version: '5.1.3.RELEASE'
    compile group: 'org.aspectj', name: 'aspectjweaver', version: '1.9.2'

    // Spring data with h2
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.1.0.RELEASE'
    runtime group: 'com.h2database', name: 'h2', version: '1.4.197'

    // Swagger
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
    
    // HttpClient (Before using microservice tools)
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.8'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.16.20'

    //For gradle 4.6 and later    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    // Business Dependencies
    compile group: 'tw.edu.ntu.csie', name: 'libsvm', version: '3.17'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.11'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.1.0.RELEASE'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'

}

jacoco {
    toolVersion = "0.8.1"
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.destination file("${buildDir}/jacocoHtml")
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['io/hychou/*.class',
                                        'io/hychou/common/*', ])
        })
    }
}

test {
    afterTest {desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
    jacoco {
        append = false
        destinationFile = file("${buildDir}/jacoco/jacocoTest.exec")
        classDumpDir = file("${buildDir}/jacoco/classpathdumps")
    }
    finalizedBy jacocoTestReport
}
